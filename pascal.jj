options {
	STATIC = false;
}

PARSER_BEGIN(Pascal)
import java.io.PrintStream; 

class Pascal {
	public RS rs = new RS();
	public CodeWriter cw = new CodeWriter();
	static public void main(String[] args)
	throws Exception {
		Pascal parser = new Pascal( System.in );
		parser.Start( System.out );
	}
	public boolean classSet = true;
}
PARSER_END(Pascal)


SKIP : { " " | "\t" | "\n" | "\r" | "\r\n" }
MORE : { < "{" > : IN_COMMENT }
<IN_COMMENT> MORE : { < "}" > : DEFAULT | < ~[] > }
TOKEN : { < PROGRAM       : "program" > }
TOKEN : { < DEBUG_ST      : "$%DEBUG_ST" > }
TOKEN : { < VAR           : "var" > }
TOKEN : { < PROCEDURE     : "procedure" > }
TOKEN : { < BEGIN         : "begin" > }
TOKEN : { < END           : "end" > }
TOKEN : { < IF            : "if" > }
TOKEN : { < THEN          : "then" > }
TOKEN : { < ELSE          : "else" > }
TOKEN : { < WHILE         : "while" > }
TOKEN : { < DO            : "do" > }
TOKEN : { < CASE          : "case" > }
TOKEN : { < OF            : "of" > }
TOKEN : { < DIV           : "div" > }
TOKEN : { < AND           : "and" > }
TOKEN : { < NOT           : "not" > }
TOKEN : { < EQUAL         : "=" > }
TOKEN : { < ASSIGN        : ":=" > }
TOKEN : { < DIFFERENT     : "<>" > }
TOKEN : { < LESS          : "<" > }
TOKEN : { < LESS_EQUAL    : "<=" > }
TOKEN : { < GREATER_EQUAL : ">=" > }
TOKEN : { < GREATER       : ">" > }
TOKEN : { < PLUS          : "+" > }
TOKEN : { < MINUS         : "-" > }
TOKEN : { < TIMES         : "*" > }
TOKEN : { < OR            : "or" > }
TOKEN : { < NUMBER        : (["0"-"9"])+ > }
TOKEN : { < IDENTIFIER    : ["a"-"z","A"-"Z", "_"] (["a"-"z","A"-"Z","0"-"9","_"])* > }
TOKEN : { < COMMA         : "," > }
TOKEN : { < SEMICOLON     : ";" > }
TOKEN : { < PERIOD        : "." > }
TOKEN : { < O_PAR         : "(" > }
TOKEN : { < C_PAR         : ")" > }
TOKEN : { < O_BRACKET     : "[" > }
TOKEN : { < C_BRACKET     : "]" > }
TOKEN : { < COLON         : ":" > }


void Start(PrintStream printStream):
{ Token t=null;}
{
	programa()
	<EOF>
	{
		if(rs.success())
			cw.printCode();
		else
			rs.printErrors();
	}
}


/* 1. <programa> ::=                                                                        */
/* program <identificador> ;                                                                */
/* <bloco>.                                                                                 */
void programa(): {}
{
  <PROGRAM>
  { 
  	if(!rs.hasErrors()) cw.putInstruction("INPP");
	}
  <IDENTIFIER>
  <SEMICOLON>
  bloco()
  <PERIOD>
  { if(!rs.hasErrors()) cw.putInstruction("PARA"); }
}


/* 2. <bloco> ::=                                                                           */
/* [<parte de declarações de variáveis>]                                                    */
/* [<parte de declarações de procedimentos>]                                                */
/* <comando composto>                                                                       */
void bloco(): { 
	int label = 0;
	boolean branchFlag = false;
}
{
	[parte_declaracao_variaveis()]
	[
	  	{ 
			if(!rs.hasErrors()) label = cw.branch();
			branchFlag = true;
		}
		parte_declaracao_procedimento()
	]
	{ 
	  if(!rs.hasErrors() && branchFlag) cw.markLabel(label);
	}
	comando_composto()
	{
		if(!rs.hasErrors()) cw.generateDeallocInstruction();
	}
}


/* 3.   <tipo> ::= <identificador>                                                          */
void tipo(): { Token t = null;}
{
	t = <IDENTIFIER> {
		rs.process("9", t);
	}
}

/* 4. <constante> ::= [+|-] (<identificador> | <numero_inteiro>)                            */
void constante(JumpTable jt): {
	Token t;
	Token signal = null;
}
{
	[
		signal = <PLUS>
		|
		signal = <MINUS>
	]
	(
		t = <IDENTIFIER> {
			rs.process("0''", t);
			rs.cacheConstant(signal, t);
		}
		|
		t = <NUMBER> {
			rs.process("0'''", t);
			rs.cacheConstant(signal, t);
		}
	)
	{ jt.putConstant(rs.value); }
}

/* 5.  <parte de declarações de variáveis> ::=                                              */
/* var <declaração de variáveis> ;{<declaração de variáveis> ;}                             */
void parte_declaracao_variaveis(): {}
{
	<VAR>
	declaracao_variaveis()
	<SEMICOLON>
	(
		declaracao_variaveis()
		<SEMICOLON>
	)*
}



/* 6. <declaração de variáveis>::=                                                          */
/* <lista de identificadores> : <tipo>                                                      */
void declaracao_variaveis(): {}
{
	lista_identificadores()
	<COLON>
	tipo() {
		rs.process("4", null);
		if(!rs.hasErrors()) cw.generateAmenInstruction();
	}
}

/* 7. <lista de identificadores> ::= <identificador> {, <identificador>}                    */
void lista_identificadores(): {
	Token t;
	int offsetAddress = 0;
}
{
	t = <IDENTIFIER> {
		if(!rs.hasErrors()) offsetAddress = cw.increaseAmenAcc(rs.currentLevel);
		rs.process("3", t, offsetAddress);
	}
	(
		<COMMA>
		t = <IDENTIFIER> {
			if(!rs.hasErrors()) offsetAddress = cw.increaseAmenAcc(rs.currentLevel);
			rs.process("3", t, offsetAddress);
		}
	)*
}


/* 8. <parte de declarações de procedimentos> ::=                                           */
/* {declaração de procedimento> ;}                                                          */
void parte_declaracao_procedimento(): {}
{
		declaracao_procedimento()
		(
		  declaracao_procedimento()
		)*
}

/* 9. <declaração de procedimento> ::=                                                      */
/* procedure <identificador>                                                                */
/* [<parâmetros formais>] ; <bloco>                                                         */
void declaracao_procedimento(): {
	Token t;
	int label = 0;
}
{
	<PROCEDURE>
	t = <IDENTIFIER> {
		rs.process("5", t);
		if(!rs.hasErrors()) label = cw.generateProcedureEntryInstruction(rs.currentLevel);
		rs.setCachedProcedureLabel(label);
	}
	[parametros_formais()]
	<SEMICOLON>
	{ 
		rs.setOffsetAddressOnParameters();
	}
	bloco()
	{ 
		rs.process("8", null);
		Symbol s = rs.getSymbolFromToken(t);
		if(!rs.hasErrors()) cw.generateProcedureReturnInstruction(s);
	}
	<SEMICOLON>
}

/* 10. <parâmetros formais> ::=                                                             */
/* (<seção de parâmetros formais> { ; <seção de parâmetros formais>} )                      */
void parametros_formais(): {}
{
	<O_PAR>
	secao_parametros_formais()
	(
		<SEMICOLON>
		secao_parametros_formais()
	)*
	<C_PAR>
}

/* 11. <seção de parâmetros formais> ::=                                                    */
/* [var] <lista de identificadores> : <identificador>                                       */
void secao_parametros_formais(): { Token t;}
{
	{ rs.process("14", null); }
	[<VAR>{ rs.process("15", null); }]
	lista_identificadores2()
	<COLON>
	t = <IDENTIFIER> {
		rs.process("19", t);
	}

}

/* 12. <comando composto> ::=  begin <comando> { ; <comando>} end                           */
void comando_composto(): {}
{
	<BEGIN>
	comando()
	(
		<SEMICOLON>
		comando()
	)*
	<END>
}

/* 13. <comado> ::=                                                                         */
/* <atribuição ou chamada de procedimento>                                                  */
/* | <comando composto>                                                                     */
/* | <comando condicional 1>                                                                */
/* | <comando condicional 2>                                                                */
/* | <comando repetitivo 1>                                                                 */
void comando(): {}
{
	atribuicao_ou_chamada_de_procedimento()
	|
	comando_composto()
	|
	comando_condicional_1()
	|
	comando_condicional_2()
	|
	comando_repetitivo_1()
}

/* 14. <atribuição ou chamada de procedimento>::= <identificador> <escolhe proced ou atrib> */
void atribuicao_ou_chamada_de_procedimento(): {Token t;}
{
	t = <IDENTIFIER> {
		rs.cachedToken = t;
	}
	escolhe_proced_ou_atrib()
}

/* 15. <escolhe proced ou atrib> ::=  <atribuição> | <chamada de procedimento>              */
void escolhe_proced_ou_atrib(): {}
{
	atribuicao()
	|
	chamada_procedimento()
}

/* 16. <chamada de procedimento> ::= [ ( <lista de expressões> ) ]                          */
void chamada_procedimento(): {}
{
	{ rs.process("21", rs.cachedToken);
		rs.process("25", rs.cachedToken);
	}
	[
		<O_PAR>
		lista_expressoes()
		<C_PAR>
	]
	{ 
		rs.process("24", rs.cachedToken);
		if(rs.cachedProcedure != null && rs.cachedProcedure.id != "read" && rs.cachedProcedure.id != "write")
			if(!rs.hasErrors()) cw.generateProcedureCallInstruction(rs.cachedProcedure);
	}
}

/* 17. <atribuição> ::= := <expressão>                                                      */
void atribuicao(): {}
{
	<ASSIGN>
	{ 
		rs.process("25'", rs.cachedToken);
		Symbol s =rs.getSymbolFromToken(rs.cachedToken);

	}
	expressao()
	{
		rs.setExpressionType();
		if(!rs.hasErrors()) cw.generateStoreInstruction(s);
	}
}

/* 18. <comando condicional 1> ::=                                                          */
/* if <expressão> then <comando>                                                            */
/* [else <comando>]                                                                         */
void comando_condicional_1(): {
	int falseLabel = 0;
	int afterElseLabel = 0;
}
{
	<IF>
	expressao()
	{
		rs.setExpressionType();
		if(!rs.hasErrors()) falseLabel = cw.branchIfFalse();
	}
	<THEN>
	comando()
	[
		LOOKAHEAD(2)
		{ if(!rs.hasErrors()) afterElseLabel = cw.branch(); }
		<ELSE>
		{ 
			if(!rs.hasErrors()) cw.putInstruction("L"+ falseLabel + " NADA");
		}
		comando()
		{ 
			if(!rs.hasErrors()) cw.putInstruction("L"+ afterElseLabel + " NADA");
		}
	]
}

/* 19. <comando condicional 2> ::=                                                          */
/* case <expressão> of <elemento do case>                                                   */
/* { ;  <elemento do case>} [else <comando>] end                                            */
void comando_condicional_2(): {
	JumpTable jt = new JumpTable();
}
{
	<CASE>
	{ cw.startRecording(); }
	expressao()
	{ 
		cw.stopRecording();
		rs.setExpressionType();	
		jt.setCaseExpressionInstructions( cw.getRecording() );
		jt.jumpTableLabel = cw.label_counter++;
		jt.afterCaseLabel = cw.label_counter++;
		
		if(!rs.hasErrors())
			cw.putInstruction("DSVS L"+jt.jumpTableLabel+"  { Go to Jump Table }");
	}
	<OF>
	elemento_case(jt)
	(
		<SEMICOLON>
		elemento_case(jt)
	)*
	{ cw.processJumpTable(jt); }
	[
		<ELSE>
		comando()
	]
	<END>
	{
		cw.putInstruction("L"+jt.afterCaseLabel+" NADA");
		rs.process("31", null);
	}
}

/* 20. <elemento do case> ::= <constante>                                                   */
/* {, constante>} : <comando>                                                               */
void elemento_case(JumpTable jt): {
	jt.addJumpLine();
}
{
	constante(jt) 
	{ 
		rs.process("30", null);
	}
	(
		<COMMA>
		constante(jt)
		{ 
			rs.process("30", null);
		}
	)*
	<COLON>
	{ if(!rs.hasErrors()) jt.setLabel(cw.markLabel()); }
	comando()
	{ if(!rs.hasErrors()) cw.putInstruction("DSVS L"+jt.afterCaseLabel); }
}

/* 21. <comando repetitivo 1> ::=                                                           */
/* while <expressão> do <comando>                                                           */
void comando_repetitivo_1(): {
	int label = 0;
	int loopLabel = 0;
}
{
	<WHILE>
	{ if(!rs.hasErrors()) loopLabel = cw.markLabel(); 
	
	}
	expressao()
	{ 
		rs.setExpressionType();
		if(!rs.hasErrors()) label = cw.branchIfFalse();
	}
	<DO>
	{
		
	}
	comando()
	{ 
		if(!rs.hasErrors()) cw.putInstruction("DSVS L"+loopLabel);
		if(!rs.hasErrors()) cw.putInstruction("L"+label+" NADA");
	}
}

/* 22. <expressão> ::= <expressão simples> [<relação> <expressão simples>]                  */
void expressao(): {
	Relation r;
}
{
	expressao_simples()
	[
		r = relacao()
		expressao_simples()
		{ 
			if(!rs.hasErrors()) cw.generateRelationInstruction(r);
		}
	]
}

/* 23. <relação> ::=   = | <> | < | <= | >= | >                                             */
Relation relacao(): {}
{
	<EQUAL> { return Relation.equal; }
	|
	<DIFFERENT> { return Relation.different; }
	|
	<LESS> { return Relation.less; }
	|
	<LESS_EQUAL> { return Relation.lessEqual; }
	|
	<GREATER_EQUAL> { return Relation.greaterEqual; }
	|
	<GREATER> { return Relation.greater; }
}


/* 24. <expressão simples> ::=                                                              */
/* [+ | -] <termo> {(+ | - | or) <termo>}                                                   */
void expressao_simples(): {
	String instruction = "";
	boolean invr = false;
}
{
	[
		<PLUS>
		|
		<MINUS> { invr = true; }
	]
	termo()
	{ if(!rs.hasErrors() && invr) cw.putInstruction("INVR"); }
	(
		(
			<PLUS> { instruction = "SOMA";}
			|
			<MINUS> { instruction = "SUBT";}
			|
			<OR> { instruction = "DISJ";}
		)
		termo()
		{ if(!rs.hasErrors()) cw.putInstruction(instruction); }
	)*
}

/* 25. <termo> ::=                                                                          */
/* <fator> {(* | div | and ) <fator> }                                                      */
void termo(): {
	String instruction = "";
}
{
	fator()
	(
		(
			<TIMES> { instruction = "MULT"; }
			|
			<DIV>  { instruction = "DIVI"; }
			|
			<AND> { instruction = "CONJ"; }
		)
		fator()
		{ if(!rs.hasErrors()) cw.putInstruction(instruction); }
	)*
}

/* 26. <fator> ::=                                                                          */
/* <variavel>                                                                               */
/* | <número>                                                                               */
/* | ( <expressão> )                                                                        */
/* | not <fator>                                                                            */
void fator(): {Token t;}
{
	variavel()
	|
	t = <NUMBER>
	{ 
		rs.process("26", t);
		rs.typeCheckOnNumericFactor(t);
		if(!rs.hasErrors()) cw.generateLoadConstant(Integer.parseInt(t.toString()));
	}
	|
	<O_PAR> expressao() <C_PAR>
	|
	<NOT> fator() { if(!rs.hasErrors()) cw.putInstruction("INVR"); }
}

/* 27. <variável> ::= <identificador>                                                       */
void variavel(): { Token t;}
{
	t = <IDENTIFIER> {

		rs.process("21", t);
		rs.typeCheckOnFactor(t);
		Symbol s = rs.getSymbolFromToken(t);
		if(!rs.hasErrors()) cw.generateLoadInstruction(s, classSet);
	}
}

/* 28. <lista de expressões> ::=  <expressão> {, <expressão>}                               */
void lista_expressoes(): {
	Symbol procedure = rs.cachedProcedure;
}
{
	{ 
		if(procedure != null && procedure.id != "write" && procedure.id != "read") {
			classSet =
				rs.cachedProcedure.parameters.get(0).classSet;
		} else if(procedure != null && procedure.id == "read") {
			if(!rs.hasErrors()) cw.putInstruction("LEIT");
		}
	}
	expressao()
	{ 
		rs.setExpressionType();
		rs.process("23", rs.cachedToken);
		if(procedure != null && procedure.id == "write") {
			if(!rs.hasErrors()) cw.putInstruction("IMPR");
		} else if(procedure != null && procedure.id == "read") {
			if(!rs.hasErrors()) cw.setReadInstruction();
		};
	}
	(
		<COMMA>
		{
			if(procedure != null && procedure.id != "write" && procedure.id != "read") {
				try {
					classSet =
					rs.cachedProcedure.parameters.get(rs.cachedParameterCount).classSet;	
				} catch(IndexOutOfBoundsException e) {
				}
			}
		}
		expressao()
		{ 
			rs.setExpressionType();
			rs.process("23", rs.cachedToken);
			if(procedure != null && procedure.id == "write") {
				if(!rs.hasErrors()) cw.putInstruction("IMPR");
			} else if(procedure != null && procedure.id == "read") {
				if(!rs.hasErrors()) cw.setReadInstruction();
			}
		}
	)*
	{ classSet = true; }
}


/* 29. <lista de identificadores2> ::= <identificador> {, <identificador>}                    */
void lista_identificadores2(): {
	Token t;
}
{
	t = <IDENTIFIER> {
		rs.process("18", t);
	}
	(
		<COMMA>
		t = <IDENTIFIER> {
			rs.process("18", t);
		}
	)*
}
